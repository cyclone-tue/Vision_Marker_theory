# Produced by CVXGEN, 2018-11-20 15:26:00 -0500.
# CVXGEN is Copyright (C) 2006-2017 Jacob Mattingley, jem@cvxgen.com.
# The code in this file is Copyright (C) 2006-2017 Jacob Mattingley.
# CVXGEN, or solvers produced by CVXGEN, cannot be used for commercial
# applications without prior written permission from Jacob Mattingley.

# Filename: description.cvxgen.
# Description: A description of the CVXGEN problem.

# some variable names are taken from paper of Mueller and D'Andrea (July 2013)

# from paper: k, A, B, z, N

dimensions
  N = 50   # 0 <= k <= maxIter
end

parameters
  A(3, 3)
  B(3, 1)
  selectVelocity(1, 3)      # [0, 1, 0]
  selectAcceleration(1, 3)  # [0, 0, 1]
  initial(3, 1)             # z[k=0]
  final(3, 1)               # z[k=N]
  min_vel # not in paper
  max_vel # not in paper
  min_acc
  max_acc
  min_jerk
  max_jerk
end

variables
  jerk[k], k = 0..N
  z[k](3,1), k = 0..N
end

minimize
  sum[k=0..N](square(jerk[k]))
end

constraints
  z[k+1] == A*z[k] + B*jerk[k], k = 0..N-1

  min_vel <= selectVelocity*z[k] <= max_vel, k = 0..N        # not in paper
  min_acc <= selectAcceleration*z[k] <= max_acc, k = 0..N
  min_jerk <= jerk[k] <= max_jerk, k = 0..N

  # Constraints of first and last elements of z:
  z[1] == initial
  z[N] == final
end
